
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	//Запросом выберем таблицу для макета и выведем их в макет
	АдресТЗ = ПолучитьДанныеДляМакета();
	
	Если АдресТЗ = Неопределено Тогда
		ОбщийМодульСервер.ДобавитьСоообщениеВмассив(, "Не заполнены данные по картам...");
		возврат;
	КонецЕсли;
	
	//Выведем таблицу в макет
	ТабДок = Новый ТабличныйДокумент;
	ЗаполнитьМакет(ТабДок, АдресТЗ);
	
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ТолькоПросмотр = Истина;
	ТабДок.Показать();
	
КонецПроцедуры


&НаСервере
Функция ПолучитьДанныеДляМакета()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Объект КАК Объект,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ДополнительныеСведения.Свойство = &НомерКарты
		|				ТОГДА ДополнительныеСведения.Значение
		|		КОНЕЦ) КАК НомерКарты,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ДополнительныеСведения.Свойство = &КодовоеСлово
		|				ТОГДА ДополнительныеСведения.Значение
		|		КОНЕЦ) КАК КодовоеСлово,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ДополнительныеСведения.Свойство = &Телефон
		|				ТОГДА ДополнительныеСведения.Значение
		|		КОНЕЦ) КАК Телефон
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	(ДополнительныеСведения.Свойство = &НомерКарты
		|			ИЛИ ДополнительныеСведения.Свойство = &КодовоеСлово
		|			ИЛИ ДополнительныеСведения.Свойство = &Телефон)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДополнительныеСведения.Объект";

		
		НомерКарты = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Номер таможенной карты");
		КодовоеСлово = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Кодовое cлово");
		Телефон = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Справоч. там. карты");
		
		Запрос.УстановитьПараметр("НомерКарты", НомерКарты);
		Запрос.УстановитьПараметр("КодовоеСлово", КодовоеСлово);
		Запрос.УстановитьПараметр("Телефон", Телефон);
		
		Результат = Запрос.Выполнить().Выгрузить();
		АдресТаблицыЗначений = ПоместитьВоВременноеХранилище(Результат);
		
		
		Если Результат.Количество() = 0 Тогда
			возврат Неопределено;
		Иначе
			возврат АдресТаблицыЗначений;
		КонецЕсли;
				
	
КонецФункции


&НаСервере
Процедура ЗаполнитьМакет(ТабДок, АдресТЗ)
	
	Таблица = ПолучитьИзВременногоХранилища(АдресТЗ);
	
	Макет = ПолучитьОбщийМакет("ТаможняСправочная");
	
	//Заголовок
	ОблЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ТабДок.Вывести(ОблЗаголовок);
	
	//Шапка
	ОблШапка = Макет.ПолучитьОбласть("Шапка");
	ТабДок.Вывести(ОблШапка);
	
	//Строки
	ОблСтрока = Макет.ПолучитьОбласть("Строка");
	Для каждого строка Из Таблица Цикл
		
		ОблСтрока.Параметры.Организация = строка.Объект;
		ОблСтрока.Параметры.НомерКарты = строка.НомерКарты;
		ОблСтрока.Параметры.КодовоеСлово = строка.КодовоеСлово;
		ТабДок.Вывести(ОблСтрока);
		
	КонецЦикла;
	
	//Подвал
	ОблПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Телефон = Таблица[0].Телефон; 	
	Телефон = Лев(Телефон, 1) + "(" + Сред(Телефон, 2, 3) + ")-" + Сред(Телефон, 5, 2) + "-" + Сред(Телефон, 7, 2) +
	                                                                                         "-" + Сред(Телефон, 9, 3);
	ОблПодвал.Параметры.Телефон = Телефон; 
	
	ТабДок.Вывести(ОблПодвал);

	
	
КонецПроцедуры
