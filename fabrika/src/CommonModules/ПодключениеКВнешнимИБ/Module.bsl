//////////////////////////////////////ПРОЦЕДУРЫ И ФУНКЦИИ СОЕДИНЕНИЯ С ВНЕШНИМИ ИБ//////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////


Процедура ПроверкаПодключения(ТипОрганизации,АдресСтруктурыПодключений,Отказ = Ложь, СтруктураПодключенийФоновая = Неопределено) экспорт
	
	СтруктураПодключений = ПолучитьИзВременногоХранилища(АдресСтруктурыПодключений);
	
	Если ТипЗнч(СтруктураПодключенийФоновая) = Тип("Структура") Тогда
		СтруктураПодключений = СтруктураПодключенийФоновая;
	КонецЕсли;	
		
	ДанныеПодключения = Новый Структура;
	Если СтруктураПодключений.Свойство(ТипОрганизации, ДанныеПодключения) Тогда
		
		Если ТипЗнч(ДанныеПодключения.Подключение) = Тип("COMОбъект") Тогда
			возврат;
		Иначе
			ДанныеПодключения.Состояние = "Не подключено";
		КонецЕсли;
		
	КонецЕсли;
	
	ПодключениеКВнешнимИБ.ДобавлениеПодключенияКБазе(ТипОрганизации,СтруктураПодключений,Отказ);		
	
	////Дима 05.03.2015 12:05:48////После изменения снова помещаем структуру подключений во временное хранилище по тому же адресу
	ПоместитьВоВременноеХранилище(СтруктураПодключений, АдресСтруктурыПодключений);
	//конец Дима
		
КонецПроцедуры

Процедура ДобавлениеПодключенияКБазе(ТипОрганизации,СтруктураПодключений,Отказ = Ложь) экспорт
	
	Отбор = Новый Структура("ТипОрганизации, ТипСоединения", ТипОрганизации, Перечисления.ТипСоединения.Com);
	ПодключениеКИБ = РегистрыСведений.НастройкаПодключенийКБазам.Получить(Отбор);
    
    //Обработаем пустые данные
    Если ПустаяСтрока(ПодключениеКИБ.СтрокаПодключения) Тогда                   
        Отказ = Истина;
		ОбщийМодульСервер.ДобавитьСоообщениеВмассив(, "Нет настройки подключения" + "	0");
		Возврат;        
    КонецЕсли;    
        
    
	СтрокаПодключения = ПодключениеКИБ.СтрокаПодключения +"Usr="+
	                    Символ(34)+ПодключениеКИБ.Пользователь+Символ(34)+";Pwd="+ПодключениеКИБ.Пароль;
	
	Попытка
		КОМ = Новый COMОбъект(Строка(ПодключениеКИБ.ТипПлатформы1С) +  ".ComConnector", ПодключениеКИБ.СерверCOMОбъекта);					
		Base = КОМ.Connect(СтрокаПодключения);
	Исключение
		Отказ = Истина;
		ОбщийМодульСервер.ДобавитьСоообщениеВмассив(, ОписаниеОшибки() + "	0");
		Возврат;
	КонецПопытки;
	
	Подключение = Новый Структура("ТипОрганизации,Подключение,Состояние",ТипОрганизации,Base,"Подключено");
	
	СтруктураПодключений.Вставить(ТипОрганизации,Подключение);	
	
	
КонецПроцедуры

