
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    
    //Установим параметры отчёта
    Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДата()));
    
    
    
	Если ЗначениеЗаполнено(Параметры.Расшифровка) Тогда
		
		Отбор = Список.Отбор;
		ПолучитьСписокОрганизаций(Параметры.Расшифровка.РасшифровкаМассив, Отбор, Параметры.Расшифровка.ДанныеРасшифровки);		
   		
	КонецЕсли;	
	
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ПолучитьСписокОрганизаций(МассивРасшифровок, Отбор, ДанныеРасшифровки, ДобавитьОрганизации = ЛОЖЬ)
	
	СтруктураРасшифровки = ОбщийМодульСервер.ПолучениеРасшифровкиВСтруктуру(МассивРасшифровок[0], ДанныеРасшифровки, Новый Структура);
    
    
    Если СтруктураРасшифровки.Свойство("ТипДвижения") Тогда
		ТипДвижения = Строка(СтруктураРасшифровки.ТипДвижения);
    Иначе
        ТипДвижения = "Списание";
    КонецЕсли;    
 
	
    Если СтруктураРасшифровки.Свойство("Период") Тогда
        //
        Группа = Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
        Группа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;    
        Группа.Использование = Истина;
        //        
        ДобавитьЭлементОтбора(Группа, "Период", СтруктураРасшифровки.Период);
        ДобавитьЭлементОтбора(Группа, "ДатаПоступления", СтруктураРасшифровки.Период);
        //
    КонецЕсли;
    
    
    Если СтруктураРасшифровки.Свойство("Организация") Тогда
        Поле = ?(ТипДвижения = "Списание", "Организация", "Получатель.ИНН");
        Значение = ?(ТипДвижения = "Списание", СтруктураРасшифровки.Организация, СтруктураРасшифровки.Организация.ИНН);
        //
		ДобавитьЭлементОтбора(Отбор, Поле, Значение);
	КонецЕсли;	
    
    
    Если СтруктураРасшифровки.Свойство("Получатель") Тогда
        Поле = ?(ТипДвижения = "Списание", "Получатель", "Организация.ИНН");
        Значение = ?(ТипДвижения = "Списание", СтруктураРасшифровки.Получатель, СтруктураРасшифровки.Получатель.ИНН);
        //
		ДобавитьЭлементОтбора(Отбор, Поле, Значение); 	
	КонецЕсли;	

		
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ДобавитьЭлементОтбора(Отбор, ЛевоеЗначение, ПравоеЗначение)
    
    
    ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
    ЭлементОтбора.ПравоеЗначение = ПравоеЗначение;
    ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЛевоеЗначение);
    ЭлементОтбора.Использование = Истина;      
   

КонецПроцедуры


