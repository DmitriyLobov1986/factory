
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявкаНаМашину") Тогда
		// Заполнение шапки
		ВалютнаяСтоимость = ДанныеЗаполнения.ВалютнаяСтоимость;
		НомерГТД = ДанныеЗаполнения.НомерГТД;
		НомерМашины = ДанныеЗаполнения.НомерМашины;
		Организация = ДанныеЗаполнения.Организация;
		ДокументОснование = ДанныеЗаполнения;
		ТипМашины = ДанныеЗаполнения.ТипМашины;
		ВидМашины = ДанныеЗаполнения.ВидМашины;
		//ОпределениеКонтрагентовИНаценки();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВидМашины"));
	
	Если РаспределениеПоПокупателям.Количество() > 0 Тогда
		ПроверяемыеРеквизиты.Добавить("РаспределениеПоПокупателям.Покупатель");
		ПроверяемыеРеквизиты.Добавить("РаспределениеПоПокупателям.Сумма");
	КонецЕсли;	

КонецПроцедуры


////Дима 28.07.2014 11:24:31////Определение покупателя и наценки
Процедура ОпределениеКонтрагентовИНаценки() экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаценкиНаТоварСрезПоследних.Поставщик,
	|	НаценкиНаТоварСрезПоследних.Покупатель,
	|	НаценкиНаТоварСрезПоследних.Наценка,
	|	НаценкиНаТоварСрезПоследних.КонтрагентЗвена
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	РегистрСведений.НаценкиНаТовар.СрезПоследних(
	|			&ДатаСреза,
	|			Организация = &Организация
	|				И ТипМашины = &ТипМашины) КАК НаценкиНаТоварСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВТ.Покупатель КАК Покупатель,
	|	ТаможняВзаимозадолженностиОбороты.СуммаДолгаПриход КАК СуммаДолгаОборот,
	|	ВТ.Поставщик,
	|	ВТ.Наценка,
	|	ВТ.КонтрагентЗвена
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТаможняВзаимозадолженности.Обороты(
	|				&НачалоМесяца,
	|				&ДатаСреза,
	|				Период,
	|				Организация = &Организация
	|					И Контрагент В
	|						(ВЫБРАТЬ
	|							ВТ.Покупатель
	|						ИЗ
	|							ВТ КАК ВТ
	|						СГРУППИРОВАТЬ ПО
	|							ВТ.Покупатель)) КАК ТаможняВзаимозадолженностиОбороты
	|		ПО ВТ.Покупатель = ТаможняВзаимозадолженностиОбороты.Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Покупатель,
	|	СуммаДолгаОборот";
	
	
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаСреза", ПолучитьГраницуПериода());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТипМашины", ТипМашины);
			
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() > 0 Тогда
		Поставщик = Результат[0].Поставщик; 
		Покупатель = Результат[0].Покупатель;
		Наценка = Результат[0].Наценка;
		КонтрагентЗвена = Результат[0].КонтрагентЗвена;
	КонецЕсли;
	
	
КонецПроцедуры

////Дима 17.07.2018 9:46:32////Определение договора "по умолчанию"
Процедура ЗаполнитьДоговорПоУмолчанию() Экспорт

  Запрос = Новый Запрос;
  Запрос.Текст = "ВЫБРАТЬ
                 |	ДоговорыКонтрагентов.Ссылка КАК Договор
                 |ИЗ
                 |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
                 |ГДЕ
                 |	ДоговорыКонтрагентов.Организация = &Организация
                 |	И ДоговорыКонтрагентов.ПоУмолчанию = ИСТИНА
                 |	И ДоговорыКонтрагентов.Владелец = &Владелец";
  
  Запрос.УстановитьПараметр("Организация", Организация);
  Запрос.УстановитьПараметр("Владелец", Поставщик);
  
  ДоговорВыборка = Запрос.Выполнить().Выбрать();
  Если ДоговорВыборка.Следующий() Тогда
	  ДоговорыКонтрагентов = ДоговорВыборка.Договор;
  КонецЕсли;	  
  

КонецПроцедуры
//конец Дима

Функция ПолучитьГраницуПериода() экспорт
	
	////Дима 12.08.2014 18:04:58////Убрал получение границы по контрагенту
	МассивПараметров = Новый Массив;
	//МассивПараметров.Добавить("Контрагент");
	МассивПараметров.Добавить("МоментВремени");
	
	ЗаписиРегистра = ОбщийМодульСервер.ПолучениеЗаписейРегистраПоРегистратору("ТаможняВзаимозадолженности", Ссылка, МассивПараметров);
	
	//Для каждого Запись Из ЗаписиРегистра Цикл
	//	Если Запись.Контрагент.Родитель = Справочники.Контрагенты.НайтиПоНаименованию("Покупатели") Тогда
	//		Граница = Новый Граница(Запись.МоментВремени, ВидГраницы.Исключая);			
	//	КонецЕсли;	
	//КонецЦикла;
	
	Если ЗаписиРегистра.Количество() > 0 Тогда 
		Граница = Новый Граница(ЗаписиРегистра[0].МоментВремени, ВидГраницы.Исключая);
		возврат Граница;
	Иначе
		возврат Дата;
	КонецЕсли;	
		
	
КонецФункции



//////////////////////////////////////ОБРАБОТКИ ПРОВЕДЕНИЯ//////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////
Процедура ОбработкаПроведения(Отказ, Режим)
	
  ПроведениеПоРегиструТаможняВзаимозадолженности();
  
  ПроведениеПоРегиструТаможняОплатаГТД();
	
КонецПроцедуры

Процедура ПроведениеПоРегиструТаможняВзаимозадолженности()

	// регистр ТаможняВзаимозадолженности Приход
	
	//Движение по поставщику
	Движения.ТаможняВзаимозадолженности.Записывать = Истина;
	Движение = Движения.ТаможняВзаимозадолженности.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Организация = Организация;
	Движение.Контрагент = Поставщик;
	Движение.ДоговорыКонтрагентов = ДоговорыКонтрагентов;
	Движение.ВалютнаяСуммаДолга = ВалютнаяСтоимость;
	Движение.ТипДвижения = "Поступление от иностранного поставщика";
	
	
	//Движение по покупателю
	Если РаспределениеПоПокупателям.Количество() > 0  Тогда
		Для Каждого строка из РаспределениеПоПокупателям Цикл
			Движение = Движения.ТаможняВзаимозадолженности.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = ?(строка.ДатаПеремещения = Дата(1,1,1), Дата, строка.ДатаПеремещения);
			Движение.Организация = строка.Организация;
			Движение.Контрагент = строка.Покупатель;
			Движение.ДоговорыКонтрагентов = строка.Договор;
			Движение.СуммаДолга = строка.Сумма;
			Движение.ТипДвижения = "Продажа покупателю в России";
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(Покупатель) И СуммаДолга > 0 Тогда
		Движение = Движения.ТаможняВзаимозадолженности.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Контрагент = Покупатель;
		Движение.СуммаДолга = СуммаДолга;
		Движение.ТипДвижения = "Продажа покупателю в России";
	КонецЕсли;		
	

	//Движение по контрагенту звена
	Если КонтрагентЗвена <> Справочники.Контрагенты.ПустаяСсылка() Тогда
		Движение = Движения.ТаможняВзаимозадолженности.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", Покупатель.ИНН);
		Движение.Контрагент = КонтрагентЗвена;
		Движение.СуммаДолга = СуммаДолга;
		Движение.ТипДвижения = "Продажа контрагенту звена";
	КонецЕсли;
	

КонецПроцедуры

Процедура ПроведениеПоРегиструТаможняОплатаГТД()

	//регистр ТаможняОплатаГТД
	Движения.ТаможняОплатаГТД.Записывать = Истина;
	
	
	////Дима 31.08.2016 10:49:57////Датой проведения сделаем дату прихода машины на склад
	ПериодЗагрузки = ?(ДокументОснование.ДатаПрихода <> Дата(1, 1, 1), ДокументОснование.ДатаПрихода, Дата);
	//конец Дима
	
	
	Если ПодИнвойсы.Количество() > 0 Тогда
		Для Каждого Строка Из ПодИнвойсы Цикл
			Движение = Движения.ТаможняОплатаГТД.Добавить();
			Движение.Период = ПериодЗагрузки;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.ГТД = Ссылка;
			Движение.ПодИнвойсНомер = Строка.НомерСтроки;
			Движение.ТипДвижения = "Приход машин";
			Движение.Сумма = Строка.Сумма;
		КонецЦикла;	
	Иначе
		Движение = Движения.ТаможняОплатаГТД.Добавить();
		Движение.Период = ПериодЗагрузки;
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.ГТД = Ссылка;
		Движение.ТипДвижения = "Приход машин";
		Движение.Сумма = ВалютнаяСтоимость;
	КонецЕсли;	

	
		
	
КонецПроцедуры
///////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////




////Дима 15.08.2014 16:08:51////Рассчитать итоговую сумму долга
Процедура РассчитатьИтоговуюСумму() Экспорт 
	
	СуммаДолга = (ВалютнаяСтоимость * КурсГТД + ТаможенныйПлатёж) * (100 + Наценка)/100;
	
КонецПроцедуры


