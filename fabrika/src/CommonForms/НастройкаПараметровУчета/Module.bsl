
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаголовокИзменен = Ложь;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщиеНастройки;
	ЗаголовокСтраницы = "Общие настройки";
	ПредыдущаяКоманда = "ОбщиеНастройки";
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Элементы.ПараметрыАдминистрированияИБ.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОграничениеДоступаНаУровнеЗаписейВключено = ТекущийОбъект.ОграничиватьДоступНаУровнеЗаписей И НЕ Константы.ОграничиватьДоступНаУровнеЗаписей.Получить();
	
	Если (ТекущийОбъект.ИспользоватьПолнотекстовыйПоиск <> Константы.ИспользоватьПолнотекстовыйПоиск.Получить()) Тогда
		
		Если НаборКонстант.ИспользоватьПолнотекстовыйПоиск Тогда
			ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Разрешить);
		Иначе
			ПолнотекстовыйПоиск.УстановитьРежимПолнотекстовогоПоиска(РежимПолнотекстовогоПоиска.Запретить);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаКлиенте
Процедура ПослеЗаписи()
	
	ОбновитьИнтерфейс();
	
	Если ЗаголовокИзменен Тогда
		ОбщегоНазначенияКлиент.УстановитьПроизвольныйЗаголовокПриложения();
		ЗаголовокИзменен = Ложь;
	КонецЕсли;
	
	Если ОграничениеДоступаНаУровнеЗаписейВключено Тогда
		Предупреждение(НСтр("ru = 'Включено ограничение доступа на уровне записей.
		                          |Требуется заполнение данных, которое будет выполняться частями
		                          |регламентным заданием ""Заполнение данных для ограничения доступа""
		                          |(ход выполнения в журнале регистрации).'"));
		Если СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая Тогда
			Предупреждение(НСтр("ru = 'В режиме файловой информационной базы регламентное задание
			                          |""Заполнение данных для ограничения доступа""
			                          |необходимо выполнить вручную через консоль регламентных заданий.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПослеЗаписи()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ЗапретЗагрузкиФайловПоРасширениюПриИзменении(Элемент)
	
	Элементы.СписокЗапрещенныхРасширений.Доступность = НаборКонстант.ЗапретЗагрузкиФайловПоРасширению;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ ПЕРЕКЛЮЧЕНИЯ МЕЖДУ ПОДСИСТЕМАМИ

&НаКлиенте
Процедура ОбщиеНастройки(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщиеНастройки;
	
	Элементы[ПредыдущаяКоманда].Шрифт = Новый Шрифт(, 8, Ложь, , , );
	
	ЗаголовокСтраницы = "Общие настройки";
	ПредыдущаяКоманда = "ОбщиеНастройки";
	
КонецПроцедуры // ОбщиеНастройки()

&НаКлиенте
Процедура ПодсистемаОрганайзер(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОрганайзер;
	Элементы.ПодсистемаОрганайзер.Пометка = Истина;
	
	Элементы[ПредыдущаяКоманда].Шрифт = Новый Шрифт(, 8, Ложь, , , );
	Элементы.ПодсистемаОрганайзер.Шрифт = Новый Шрифт(, 8, Истина, , , );
	
	ЗаголовокСтраницы = "Органайзер";
	ПредыдущаяКоманда = "ПодсистемаОрганайзер";
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсистемаОбменДанными(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбменДанными;
	Элементы.ПодсистемаОбменДанными.Пометка = Истина;
	
	Элементы[ПредыдущаяКоманда].Шрифт = Новый Шрифт(, 8, Ложь, , , );
	Элементы.ПодсистемаОбменДанными.Шрифт = Новый Шрифт(, 8, Истина, , , );
	
	ЗаголовокСтраницы = "Обмен данными";
	ПредыдущаяКоманда = "ПодсистемаОбменДанными";
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиКриптографии(Команда)
	ПараметрыФормы = Новый Структура("ПоказыватьНастройкиШифрования", Истина);
	ОткрытьФормуМодально("Общаяформа.НастройкиЭЦП", ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЭлектронныеЦифровыеПодписиПриИзменении(Элемент)
	Элементы.НастройкиЭЦП.Доступность = НаборКонстант.ИспользоватьЭлектронныеЦифровыеПодписи;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД

&НаКлиенте
Процедура ЗаголовокСистемыПриИзменении(Элемент)
	ЗаголовокИзменен = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНомераОтсканированныхФайлов(Команда)
	ОткрытьФорму("РегистрСведений.НомераСканированныхФайлов.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура ФункциональнаяОпцияДополнительныеСведенияПриИзменении(Элемент)
	
	Если НаборКонстант.ИспользоватьОбменССайтами
		И НЕ НаборКонстант.ИспользоватьДополнительныеРеквизитыИСведения Тогда
		
		НаборКонстант.ИспользоватьДополнительныеРеквизитыИСведения = Истина;
		
		Предупреждение(НСтр("ru = 'Использование дополнительных реквизитов и сведений не может быть отключено,
		|т.к. включен обмен данными с сайтами.'"));
		
	КонецЕсли;
	
КонецПроцедуры

